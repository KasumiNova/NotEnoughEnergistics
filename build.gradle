buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = mod_version
group = mod_group
archivesBaseName = mod_archives_name

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    version = "1.12.2-14.23.5.2847"
    mappings = "stable_39"
    runDir = "run"

    replace '@VERSION@', project.version

    clientJvmArgs += "-Dfml.coreMods.load=com.github.vfyjxf.nee.asm.NEELoadingPlugin"
    serverJvmArgs += "-Dfml.coreMods.load=com.github.vfyjxf.nee.asm.NEELoadingPlugin"
}

processResources {

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version,
                'mod_id': mod_id,
                'mod_name': mod_name,
                'mod_credits': mod_credits,
                'mod_author': mod_author,
                'mod_description': mod_description,
                'mc_version': mc_version,
                'mod_version': mod_version,
                'mod_logo': mod_logo
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

}

repositories {
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        name = "covers Maven"
        url = "https://maven.covers1624.net"
    }
    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com"
    }
    maven { url = "https://maven.cil.li/" }
    flatDir {
        dirs 'libs'
    }
}

dependencies {

    deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}"
//    compile "curse.maven:modularui-624243:3895901"
    //    AE2 Unofficial Extended Life
//    deobfCompile "curse.maven:ae2-extended-life-570458:4264378"
    deobfCompile "curse.maven:applied-energistics-2-223794:2747063"
    deobfCompile "curse.maven:wireless-crafting-terminal-244559:2830252"
    compile "curse.maven:ae2wtlib-304024:2830114"
    compile "curse.maven:p455w0rds-library-255232:2830265"
//    deobfCompile "p455w0rd:WirelessCraftingTerminal:1.12.2-3.12.97"
//    deobfCompile "p455w0rd:AE2WTLib:1.12.2-1.0.34"
//    deobfCompile "p455w0rd:p455w0rdslib:1.12.2-2.3.161"
    //Big orediect test
    deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.680"

}

task devJar(type: Jar) {
    classifier = 'dev'
    from sourceSets.main.output
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

runClient {
    args '--username', 'developer'
}

jar {
    appendix project.minecraft.version
    manifest.attributes(
            'FMLAT': 'neenergistics_at.cfg',
            'FMLCorePlugin': 'com.github.vfyjxf.nee.asm.NEELoadingPlugin',
            'FMLCorePluginContainsFMLMod': 'true'
    )
}

artifacts {
    archives devJar
    archives sourcesJar
}